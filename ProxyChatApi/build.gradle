/*
 * ProxyChat, a Velocity chat solution
 * Copyright (C) 2020 James Lyne
 *
 * Based on BungeeChat2 (https://github.com/AuraDevelopmentTeam/BungeeChat2)
 * Copyright (C) 2020 Aura Development Team
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'io.freefair.lombok'
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'
    compileOnly 'org.spongepowered:configurate-hocon:3.6'
    compileOnly 'org.spongepowered:configurate-yaml:3.6'
    compileOnly 'net.luckperms:api:5.0'

    annotationProcessor 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'

    testImplementation 'junit:junit:4.13'
    testImplementation 'ch.vorburger.mariaDB4j:mariaDB4j:2.3.0'
    testImplementation 'commons-io:commons-io:2.6'

    testCompileOnly 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'

    testRuntimeOnly 'org.mariadb.jdbc:mariadb-java-client:2.4.1'
}


group = 'uk.co.notnull'
version = '1.0-SNAPSHOT'

description = 'ProxyChat API'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                description = 'ProxyChat API'
                url = 'https://github.com/JLyne/ProxyChat'
                developers {
                    developer {
                        id = 'jim'
                        name = 'James Lyne'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/JLyne/ProxyChat.git'
                    developerConnection = 'scm:git:ssh://github.com/JLyne/ProxyChat.git'
                    url = 'https://github.com/JLyne/ProxyChat'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = rootProject.ext.mavenUserName
                password = rootProject.ext.mavenPassword
            }
            authentication {
                basic(BasicAuthentication)
            }

            def releasesRepoUrl = "https://repo.not-null.co.uk/releases/" // gradle -Prelease publish
            def snapshotsRepoUrl = "https://repo.not-null.co.uk/snapshots/"
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}
